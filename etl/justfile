# Global dirs
data_gen_root_dpath := justfile_directory() / ".."
data_root_dpath := data_gen_root_dpath / ".."

# Staging dirs
staging_dpath := data_root_dpath / "staging"

clean_and_prep:
    #!/usr/bin/env bash
    set -euo pipefail
    if [ -d "{{staging_dpath}}" ]; then
        rm -rf "{{staging_dpath}}"
    fi
    mkdir -p "{{staging_dpath}}"

gen: clean_and_prep gen_rules gen_bestiary gen_adventures gen_unified gen_unified_dse

gen_rules:
    #!/usr/bin/env bash
    set -euo pipefail
    cp "{{data_gen_root_dpath}}/Rules/Draw Steel Rules.md" "{{staging_dpath}}/Draw Steel Rules.md"
    just -f preformat/justfile run "{{staging_dpath}}/Draw Steel Rules.md"
    just -f rules.justfile gen_rules

# TODO - support bestiary
gen_bestiary:
    #!/usr/bin/env bash
    set -euo pipefail
    exit
    just -f bestiary.justfile gen_bestiary

gen_adventures:
    just -f adventures.justfile gen_adventures

gen_unified:
    just -f unified.justfile gen_unified

gen_unified_dse:
    just -f unified-dse.justfile gen_unified_dse

# make the headers match the bookmarks
one_off_fix_headers_of_src:
    # Generates the output doc in staging, then compare it, then copy it to src if its good
    just -f fix_md_headers/justfile run "{{data_gen_root_dpath}}/Rules/bookmarks.txt" "{{data_gen_root_dpath}}/Rules/Draw Steel Rules.md" "{{staging_dpath}}/Draw Steel Rules.md"


# deletes all the (non-hidden) files in a directory
_delete_dir_except_git dir_to_delete:
    #!/usr/bin/env bash
    set -euo pipefail
    tempdir="$(mktemp -d)"
    echo "$tempdir"
    # add a dummy file to avoid crashing in case the dir is empty
    touch "{{dir_to_delete}}/dummy.txt"
    mv "{{dir_to_delete}}"/* "${tempdir}/"
    # by default dotglob will prevent hidden files from being included in * wildcard globs
    if [ -d "${tempdir}/.git" ]; then
        mv "${tempdir}/.git" "{{dir_to_delete}}"
    fi
    if [ -f "${tempdir}/README.md" ]; then
        mv "${tempdir}/README.md" "{{dir_to_delete}}"
    fi
    rm -rf "$tempdir"

_add_license dir:
    #!/usr/bin/env bash
    set -euo pipefail
    cd "{{dir}}"
    echo "The Steel Compendium is an independent product published under the DRAW STEEL Creator License and is not affiliated with MCDM Productions, LLC. DRAW STEEL Â© 2024 MCDM Productions, LLC." > LICENSE

switch_repos_to branch:
    #!/usr/bin/env bash
    set -euo pipefail
    #cd "{{data_root_dpath}}"

    find "{{data_root_dpath}}" -type d -name "data-*" | while read -r data_repo_dpath; do
        cd "$data_repo_dpath"
        if [ ! -d ".git" ]; then
            echo >&2 "Directory is not a git repo: $data_repo_dpath"
            exit 1
        fi
        if [[ "$data_repo_dpath" == *data-gen ]]; then
            echo >&2 "Skipping: $data_repo_dpath"
        else
            echo >&2 "Switching git repo to '{{branch}}' branch: $data_repo_dpath"
            git checkout "{{branch}}"
        fi
    done
