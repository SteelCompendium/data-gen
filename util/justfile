export PATH := "./node_modules/.bin:" + env_var('PATH')

generate:
    just generate_html_pandoc
    just generate_abilities
    just generate_movement
    just generate_kits
    just generate_ancestries
    just generate_careers
    just generate_cultures
    just generate_complications
    just generate_conditions
    just generate_skills
    just format_ability_tables
    just format_markdown
    just auto_link

generate_html_pandoc:
    #!/usr/bin/env bash
    set -euo pipefail
    echo >&2 "[JUST] --- Generating html --- "

    # convert from markdown to html
    pandoc --wrap=none \
        --standalone \
        --section-divs \
        -o "../Rules/Draw Steel Rules.html" \
        "../Rules/Draw Steel Rules.md"

    # avoiding some cdata mess
    sed -i 's/<style>/<style type="text\/css">/g' "../Rules/Draw Steel Rules.html"

    # tidy up the html
    tidy -i "../Rules/Draw Steel Rules.html" > .tmp || true
    mv .tmp "../Rules/Draw Steel Rules.html"

    # Replace html entities
    sed -i 's/&#8220;/"/g; s/&#8221;/"/g; s/&amp;/&/g; s/&lt;/</g; s/&gt;/>/g; s/\&nbsp\;/ /g;' "../Rules/Draw Steel Rules.html"
    sed -i "s/&#8217;/'/g; s/â€™/'/g;" "../Rules/Draw Steel Rules.html"

generate_abilities:
    #!/usr/bin/env bash
    set -euo pipefail

    abilities_path="../Abilities"

    # Conduit
    just generate_section "$abilities_path/Conduit" "//section[@id='conduit-abilities']//section[@class='level6']"
    just generate_section "$abilities_path/Conduit/Triggered Actions" "//section[@id='conduit']//section[@id='triggered-action']//section[@class='level5']"

    # Elementalist
    just generate_section "$abilities_path/Elementalist" "//section[@id='elementalist-abilities']//section[@class='level6']"
    just generate_section "$abilities_path/Elementalist/Triggered Actions" "//section[@id='elementalist']//section[@id='specialization-triggered-action']//section[@class='level5']"

    # Fury
    just generate_section "$abilities_path/Fury" "//section[@id='fury-abilities']//section[@class='level6']"
    just generate_section "$abilities_path/Fury/Triggered Actions" "//section[@id='fury']//section[@id='aspect-triggered-action']//section[@class='level5']"
    rm "$abilities_path/Fury/Triggered Actions/Fury Triggered Actions.md"

    # Shadow
    just generate_section "$abilities_path/Shadow" "//section[@id='shadow-abilities']//section[@class='level6']"
    just generate_section "$abilities_path/Shadow/Triggered Actions" "//section[@id='shadow']//section[@id='college-triggered-action']//section[@class='level5']"
    rm "$abilities_path/Shadow/Triggered Actions/Shadow Triggered Actions.md"

    # Tactician
    just generate_section "$abilities_path/Tactician" "//section[@id='tactician-abilities']//section[@class='level6']"
    just generate_section "$abilities_path/Tactician/Triggered Actions" "//section[@id='tactician']//section[@id='doctrine-triggered-action']//section[@class='level5']"
    rm "$abilities_path/Tactician/Triggered Actions/Tactician Triggered Actions.md"

    # Common Maneuvers
    just generate_section "$abilities_path/Common Maneuvers" "//section[@id='combat']//section[@id='maneuvers']//section[@class='level4']"

    # Common Actions
    just generate_section "$abilities_path/Common Actions" "//section[@id='combat']//section[@id='actions']//section[@class='level4']"

generate_movement:
    #!/usr/bin/env bash
    set -euo pipefail

    movement_path="../Movement"

    xpath="//section[@id='combat']//section[@id='movement']//section[@class='level4']"
    just generate_section "$movement_path" "$xpath"
    xpath="//section[@id='combat']//section[@id='movement-types']//section[@class='level5']"
    just generate_section "$movement_path" "$xpath" "skip_delete"

    rm "${movement_path}/Cant Exceed Speed.md"
    rm "${movement_path}/Movement Types.md"

generate_kits:
    #!/usr/bin/env bash
    set -euo pipefail

    martial_path="../Kits/Martial Kits"
    xpath="//section[@id='kits']//section[@id='martial-kits']//section[@class='level4']"
    just generate_section "$martial_path" "$xpath"

    caster_path="../Kits/Caster Kits"
    xpath="//section[@id='kits']//section[@id='caster-kits']//section[@class='level4']"
    just generate_section "$caster_path" "$xpath"

generate_ancestries:
    #!/usr/bin/env bash
    set -euo pipefail
    ancestries_path="../Ancestries"
    xpath="//section[@id='ancestries']//section[@class='level3']"
    just generate_section "$ancestries_path" "$xpath"

    # removes any extra sections
    rm "${ancestries_path}/Starting Size And Speed.md"
    rm "${ancestries_path}/On The Origin Of Species.md"

generate_careers:
    #!/usr/bin/env bash
    set -euo pipefail
    careers_path="../Careers"
    xpath="//section[@id='career-1']//section[@class='level3']"
    just generate_section "$careers_path" "$xpath"

    # removes any extra sections
    rm "${careers_path}/Career Benefits.md"
    rm "${careers_path}/Career Questions.md"
    rm "${careers_path}/Inciting Incident.md"

generate_cultures:
    #!/usr/bin/env bash
    set -euo pipefail
    cultures_path="../Cultures"

    xpath="//section[@id='culture-1']//section[@id='culture-benefits']//section[@id='environment']//section[@class='level6']"
    just generate_section "$cultures_path/Environments" "$xpath"

    xpath="//section[@id='culture-1']//section[@id='culture-benefits']//section[@id='organization']//section[@class='level5']"
    just generate_section "$cultures_path/Organizations" "$xpath"

    xpath="//section[@id='culture-1']//section[@id='culture-benefits']//section[@id='upbringing']//section[@class='level5']"
    just generate_section "$cultures_path/Upbringing" "$xpath"

    # removes any extra sections
    rm "${cultures_path}/Upbringing/But I Really Want Alertness.md"

generate_complications:
    #!/usr/bin/env bash
    set -euo pipefail
    complications_path="../Complications"
    xpath="//section[@id='complications']//section[@id='determine-complication']//section[@class='level4']"
    just generate_section "$complications_path" "$xpath"
    rm "${complications_path}/Complications Table.md"

generate_skills:
    #!/usr/bin/env bash
    set -euo pipefail
    skills_path="../Skills"
    xpath="//section[@id='tests']//section[@id='skill-groups']//section[@class='level5']"
    just generate_section "$skills_path" "$xpath"

    # removes any extra sections
    rm "${skills_path}/Are All Skills Equal?.md"
    rm "${skills_path}/For The Director: Make Your Own Skills.md"

generate_conditions:
    #!/usr/bin/env bash
    set -euo pipefail
    conditions_path="../Conditions"
    xpath="//section[@id='abilities']//section[@id='conditions']//section[@class='level4']"
    just generate_section "$conditions_path" "$xpath"

generate_section destination_path xpath skip_delete="true":
    #!/usr/bin/env bash
    set -euo pipefail
    echo >&2 "[JUST] --- Generating {{destination_path}} --- "
    source ./util.bash

    if [ "{{skip_delete}}" == "true" ]; then
        rm -rf "{{destination_path}}"
    else
        echo "SKIP DELETE"
    fi

    python3 ./query.py "{{xpath}}" "../Rules/Draw Steel Rules.html" "{{destination_path}}"
    html_folder_to_md "{{destination_path}}"

format_ability_tables:
    #!/usr/bin/env bash
    set -euo pipefail
    echo >&2 "[JUST] --- Formatting ability tables --- "

    python3 format_ability_tables.py "../"

format_markdown:
    #!/usr/bin/env bash
    set -euo pipefail
    echo >&2 "[JUST] --- Formatting Markdown --- "
    mdformat --version
    for dir in ../*/ ; do
        if [ "$dir" != "../Rules/" ] && [ "$dir" != "../util/" ]; then
            mdformat "$dir"
        fi
    done

auto_link:
    #!/usr/bin/env bash
    set -euo pipefail
    echo >&2 "[JUST] --- Linking generated notes to each other --- "

    # Prepare the linked rules file
    mkdir -p "../Formatted"
    cp "../Rules/Draw Steel Rules.md" "../Formatted/Formatted Draw Steel Rules.md"

    python3 obs-auto-linker.py ../

    mv "../Formatted/Formatted Draw Steel Rules.md" "../Rules/Draw Steel Rules - Linked.md"
    rm -rf "../Formatted"
